!-------------------------------------- LICENCE BEGIN ------------------------------------
!Environment Canada - Atmospheric Science and Technology License/Disclaimer, 
!                     version 3; Last Modified: May 7, 2008.
!This is free but copyrighted software; you can use/redistribute/modify it under the terms 
!of the Environment Canada - Atmospheric Science and Technology License/Disclaimer 
!version 3 or (at your option) any later version that should be found at: 
!http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html 
!
!This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; 
!without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. 
!See the above mentioned License/Disclaimer for more details.
!You should have received a copy of the License/Disclaimer along with this software; 
!if not, you can write to: EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec), 
!CANADA, H9P 1J3; or send e-mail to service.rpn@ec.gc.ca
!-------------------------------------- LICENCE END --------------------------------------
SUBROUTINE HYDRO_SVS ( DT, &
     EG, ER, ETR, RR, RSNOW, RSNOWV, &
     IMPERVU, VEGL, VEGH, PSN, PSNVH, ACROOT, WRMAX, &
     WSAT, KSAT, PSISAT, BCOEF, FBCOF, WFCINT, GRKEF, &
     SNM, SVM, WR, WRT, WD, WDT, WF, &
     KSATC, KHC, PSI, GRKSAT, WFCDP, &
     F, LATFLW, RUNOFF, N)
  !
  use sfc_options
  use svs_configs
  implicit none
#include <arch_specific.hf>

  !     
  INTEGER N,K 
  ! CONSTANTS for horizontal decay of GRKSAT
  ! 
  REAL, PARAMETER :: GRKSAT_C1=10.0
  REAL, PARAMETER :: GRKSAT_C2=5.0


  REAL DT

  ! input
  real, dimension(n)        :: eg, er, etr, rr, impervu
  real, dimension(n)        :: psn, psnvh, vegh, vegl
  real, dimension(n,nl_svs) :: bcoef, fbcof, acroot, ksat
  real, dimension(n,nl_svs) :: psisat, wfcint, wsat
  real, dimension(n)        :: grkef, rsnow, rsnowv, wrmax, snm, svm 
  ! prognostic vars (I/0)
  real, dimension(n)        :: wr, wrt
  real, dimension(n,nl_svs) :: wd , wdt, wf
  ! output
  real, dimension(n,nl_svs) :: grksat, ksatc
  real, dimension(n,nl_svs-1):: khc, psi
  real, dimension(n)        :: wfcdp
  real, dimension(n,nl_svs+1):: f
  real, dimension(n,nl_svs) :: latflw
  real, dimension(n)        :: runoff

  !
  !Author
  !          S. Belair, M.Abrahamowicz, S.Z.Husain, N.Alavi, S.Zhang (June 2015) 
  !Revisions
  !
  ! 016       S. Zhang (May 2013)
  !           - Modified the calculation of volumetric water content in the 
  !             deep soil layers according to Boone (1999)
  ! 017       N. Alavi (April 2014)
  !           - New calculation for soil water content evolution, lateral flow, runoff, and baseflow
  ! 018       E. Gaborit (Nov. 2015)
  !           - Modify runnoff for urban areas
  ! 019       M. Abrahamowicz and V. Fortin (Sept. 2016)
  !           - Clean up the code
  ! 020       V. Fortin (Oct. 2016)
  !           - Implement a Runge-Kutta solver
  !           - Receive active root fraction per layer instead of cumulative unstressed root fraction
  !
  !Object
  !     Calculates the evolution of the soil water contents
  !     liquid water retained in the vegetation canopy (Wr).  
  !     Also determine the runoff, lateral flow and drainage
  !     using interflow parametrization for sloping train
  !     (based on Soulis et al. 2000 and 2011)
  !
  !Arguments
  !
  !          - INPUT -
  !
  ! DT       timestep
  !
  !          --- Precipitation and Evaporation Rates ---
  !
  ! EG             evaporation rate (no fraction) over bare ground (1st soil layer) [kg/m2/s]
  ! ER             direct evaporation rate (no fraction) from veg. leaves [kg/m2/s]
  ! ETR            evapotranspiration rate (no fraction) [kg/m2/s]
  ! RR             rain rate at the surface [kg/m2/s]
  ! RSNOW          flow of liquid water through the bare ground/low veg. snow pack - to the soil [kg/m2/s]
  ! RSNOWV         flow of liquid water through the snow-under-veg pack - to the soil [kg/m2/s]
  !
  !          --- (Surface) Cover Fraction  ---
  !
  ! IMPERVU        fraction of land surface that is considered impervious (related to urban areas) [0-1]
  ! VEGL           fraction of LOW vegetation [0-1]
  ! VEGH           fraction of HIGH vegetation [0-1]
  ! PSN            fraction of bare ground or low veg. covered by snow [0-1]
  ! PSNVH          fraction of HIGH vegetation covered by snow [0-1]
  !
  !          --- Vegetation characteristics ---
  !
  ! ACROOT(NL_SVS) active root fraction in each soil layer [0-1]
  !                (ETR is multiplied by ACROOT to get ETR for each layer)
  ! WRMAX          max water content retained on vegetation [kg/m2]
  !
  !          --- Soil characteristics ---
  !
  ! WSAT  (NL_SVS) volumetric water content at soil saturation per layer [m3/m3]
  ! KSAT  (NL_SVS) vertical hydraulic conductivity at saturation per layer [m/s]
  ! PSISAT(NL_SVS) value of soil water suction at air-entry (near saturation) per layer [m]
  ! BCOEF (NL_SVS) slope of the retention curve per layer
  ! FBCOF (NL_SVS) parameter derived from BCOEF per layer to determine field capacity (Soulis et al. 2011)
  ! WFCINT(NL_SVS) volumetric water content at field capacity for interflow (per layer) [m3/m3]
  ! GRKEF          ratio of tile slope to tile length (needed for watdrain to calculate lateral flow) [m-1]
  !
  !          --- Prognostic variables of SVS not modified by HYDRO_SVS ---
  !
  ! SNM           snow water equivalent (SWE) for bare ground and low veg snow [kg/m2]
  ! SVM           snow water equivalent (SWE) for snow under high veg [kg/m2]
  !
  !          - INPUT/OUTPUT  -
  !
  !          --- Prognostic variables of SVS modified by HYDRO_SVS ---
  !
  ! WR             water content retained by vegrtation canopy [kg/m2]
  ! WRT            new water content retained by vegetation canopy [kg/m2]
  ! WD (NL_SVS)    soil volumetric water content (per layer) [m3/m3]
  ! WDT(NL_SVS)    new soil volumetric water content (per layer) [m3/m3]
  ! WF (NL_SVS)    frozen soil volumetric water (per layer) [m3/m3]
  !
  !          -  OUTPUT  -
  !
  !          ---  Diagnostic Soil Parameters (used only for testing SVS) ---
  !
  ! KSATC (NL_SVS) KSAT adjusted for presence of ice (per layer) [m/s]
  ! KHC (NL_SVS-1) soil hydraulic conductivity at soil boundaries (of layer) [m/s]
  ! PSI (NL_SVS-1) soil water potential at the soil boundaries (of layer) [m]
  ! GRKSAT(NL_SVS) horizontal hydraulic conductivity at saturation (per layer) [m/s]
  ! WFCDP          volumetric water content at field capacity at the bottom of the permeable soil [m3/m3]
  !
  !          ---  Soil water fluxes and Runoff ---
  !
  ! F(NL_SVS+1)    water flux between soil layers and through bottom (in meters, converted to mm at the end ) [kg/m2/dt]
  ! LATFLW(NL_SVS) lateral flow (interflow) (in meters in calculation, converted to mm at the end) per layer [kg/m2/dt]
  ! RUNOFF         surface runoff [kg/m2/dt]
  !
  !          -  DIMENSIONS  -
  !
  ! N              number of grid cells
  !
  include "isbapar.cdk"
  include "thermoconsts.inc"
  !
  INTEGER I

  ! local arrays


  real, dimension(n,nl_svs)   :: delzvec
  real, dimension(n,nl_svs)   :: asatfc, wsatc, asat0, grkefl
  real, dimension(n,nl_svs)   :: etr_grid
  real, dimension(n)          :: asat1, basflw, satsfc, subflw , pg, rveg

  ! For the Runge-Kutta method
  real, dimension(n,nl_svs)   :: wd_rk, dwd_rk1, dwd_rk2, dwd_rk3, dwd_rk4
  real, dimension(n,nl_svs+1) :: f_rk

  !***********************************************************************
  !
  !   0.        INITIALIZE TO ZERO THE VERTICAL AND LATER FLUXES
  !

  DO I=1,N
     DO K=1,NL_SVS
        LATFLW(I,K)=0.
        F(I,K)=0.
     ENDDO
     F(I,NL_SVS+1)=0.
  ENDDO
  !
  !-------------------------------------
  !   1.        EVOLUTION OF THE EQUVALENT WATER CONTENT Wr
  !      
  !     
  !                                  Remove evaporation from canopy reservoir
  !                                  Then add precipitation. Careful: rain falls
  !                                  on canopy only if there is no snow under the
  !                                  vegetation. If there is snow on the ground,
  !                                  all rain goes directly to the snowpack and no
  !                                  liquid water remains in the vegetation
  !
  DO I=1,N
     !

     IF (SVM(I).GE.CRITSNOWMASS)THEN
        !                                  There is snow on the ground, remove evaporation
        !                                  then drain all liquid water from the vegetation
        WRT(I) = WR(I) - DT * ER(I)
        !                                  Liquid water in vegetation becomes runoff
        RVEG(I) = MAX(0.0,WRT(I)/DT) ! Runoff mult. by dt later on, so water balance maintained..
        WRT(I) = 0.0

     ELSE
        !                                  Remove P-E from liquid water in vegetation
        !                                  Sanity check: WRT must be positive
        !                                  since ER is limited to WR/DT+RR in ebudget
        WRT(I) = MAX(0., WR(I) - DT * (ER(I) -  RR(I)))
        !                                  Compute canopy drip
        !                                  if Wr > Wrmax, there is runoff
        RVEG(I) = MAX(0., (WRT(I) - WRMAX(I)) / DT ) 
        !
        !                                  Wr must be smaller than Wrmax
        !                                  after runoff is removed
        !
        WRT(I) = MIN(WRT(I), WRMAX(I))
     END IF
     !
  END DO
  !
  !                                  Compute precipitation reaching the ground PG
  !                                  as a weighted average of RVEG and RR
  !                                  if there is no snow on bare ground,
  !                                  otherwise PG is just from RVEG since RR goes
  !                                  through the snowpack
  !                                   
  DO I=1,N
     !   
     IF (SNM(I).GE.CRITSNOWMASS)THEN
        PG(I) = (VEGL(I) + VEGH(I)) * RVEG(I)
     ELSE
        PG(I) = (VEGL(I) + VEGH(I)) * RVEG(I) & 
             + (1. - VEGL(I) - VEGH(I) ) * (1. - PSN(I)) * RR(I)
     ENDIF
     !
  END DO
  !
  !

  !
  !        2.     ADD EFFECT OF SNOWPACK RUNOFF 
  !               ------------------------------------------
  !
  !                               Include the effect of runoff from the
  !                               snowpack as a source for water reaching
  !                               the ground
  !
  DO I=1,N
     PG(I) = PG(I) + (1.-VEGH(I)) * RSNOW(I) &   
          +    VEGH(I) * RSNOWV(I)
  END DO

  !
  !
  !        3.     CALCULATE THE REQUIRED PARAMETERS FOR NEW HYDRLOGY ROUTINE 
  !               ------------------------------------------

  DO I=1,N
     DO K=1,NL_SVS
        !Vectorize layer thicknesses over space for watdrn module
        DELZVEC(I,K)=DELZ(K)

        !Adjust ksat and wsat for presence of ice
        KSATC(I,K)=KSAT(I,K)*(1.0-MAX(0.0,MIN((WSAT(I,K)-CRITWATER)/WSAT(I,K),WF(I,K)/WSAT(I,K))))**2. 
        WSATC(I,K)= MAX((WSAT(I,K)-WF(I,K)-0.00001), CRITWATER)

        ! Calculate parameters needed for WATDRAIN
        ! ASAT0  bulk saturation at the begining of time step (WD/WDSAT)

        ASAT0 (I,K) = WD(I,K) / WSATC(I,K)
        ASATFC(I,K) = WFCINT(I,K)/ WSAT(I,K)
     END DO
  END DO

  !            Horizontal soil hydraulic conductivity decays with depth 
  !             down to KDP (last active layer) and then is fixed to KDP layer values.
  DO I=1,N
     DO K=1,KDP
        ! Horizontal soil hydraulic conductivity decays with depth
        GRKSAT (I,K) = GRKSAT_C1 * EXP( GRKSAT_C2 *(DL_SVS(KDP)-DL_SVS(K))/DL_SVS(KDP))*KSATC(I,K)
        GRKEFL (I,K) = GRKEF(I)*GRKSAT(I,K)
     END DO
     DO K=KDP+1,NL_SVS
        ! set Horizontal soil hydraulic conductivity for layers below KDP to the KDP$
        GRKSAT (I,K) = GRKSAT(I,KDP)
        GRKEFL (I,K) = GRKEF(I)*GRKSAT(I,K)
     END DO
  END DO

  !        4.      CALCULATE SURFACE RUNOFF
  !              ------------------------------------------
  !Call watdrain to calculate runoff


  CALL RUNSVSDRN(DELZVEC(:,1),BCOEF(:,1),WSATC(:,1),GRKSAT(:,1),GRKEFL(:,1),ASATFC(:,1),ASAT0(:,1),ASAT1,SUBFLW,BASFLW,SATSFC,N,1,N,DT)


  DO I=1,N
     !use ksat to calculate runoff (mm/s)
     RUNOFF(I) = MAX( (SATSFC(I)*PG(I)+(1-SATSFC(I))*MAX(PG(I)-KSATC(I,1)*1000.,0.0)) , 0.0 )             
     ! assuming that 33% of urban cover is totally impervious (RUNOFF = PG over impervious surface)
     RUNOFF(I) = RUNOFF(I) * (1. - IMPERVU(I) ) + PG(I) * IMPERVU(I)        
     ! remove runoff from the ampount of water reaching the ground
     PG(I) = PG(I) - RUNOFF(I)              ! (mm/s)
     RUNOFF(I) = RUNOFF(I)*DT
  END DO


  !        5.      CALCULATE BASEFLOW AT THE BOTTOM OF THE LAST ACTIVE AND LAST LAYERS
  !              -----------------------------------------

  ! BASE FLOW for the active layer KDP
  ! Compute wfc as a function of thickness of the permeable depth based on Soulis et al. (2012)

  DO I=1,N
     WFCDP(I) = WSATC(I,KDP)*FBCOF(I,KDP)*(PSISAT(I,KDP)/DL_SVS(KDP))**(1./BCOEF(I,KDP))
  END DO

  !Call WATDRAIN to calculate baseflow

  CALL RUNSVSDRN(DELZVEC(:,KDP),BCOEF(:,KDP),WSATC(:,KDP),KSATC(:,KDP), &
       GRKEFL(:,KDP),ASATFC(:,KDP),ASAT0(:,KDP),ASAT1,SUBFLW,BASFLW,SATSFC,N,1,N,DT)

  DO I=1,N

     IF(WD(I,KDP).GT.WFCDP(I)) THEN  
        !baseflow only happens when soil water contant of last layer exceeds field capacity
        F(I,KDP+1)=BASFLW(I)
     ELSE 
        F(I,KDP+1)=0.0
     END IF

  END DO

  ! BASE FLOW FOR THE LAST LAYER NL_SVS IF KDP.NE.NL_SVS
  ! Compute wfc as a function of thickness of the total depth DL_SVS(NL_SVS) base on Soulis et al. (2012)
  IF(KDP.NE.NL_SVS) THEN 

     DO I=1,N
        WFCDP(I) = WSATC(I,NL_SVS)*FBCOF(I,NL_SVS)*(PSISAT(I,NL_SVS)/DL_SVS(NL_SVS))**(1./BCOEF(I,NL_SVS))
     END DO

     !Call WATDRAIN to calculate baseflow

     CALL RUNSVSDRN(DELZVEC(:,NL_SVS),BCOEF(:,NL_SVS),WSATC(:,NL_SVS),KSATC(:,NL_SVS),&
          GRKEFL(:,NL_SVS),ASATFC(:,NL_SVS),ASAT0(:,NL_SVS),ASAT1,SUBFLW,BASFLW,SATSFC,N,1,N,DT)

     DO I=1,N
        IF(WD(I,NL_SVS).GT.WFCDP(I)) THEN
           !baseflow only happens when soil water contant of last layer exceeds field cap$
           F(I,NL_SVS+1)=BASFLW(I)
        ELSE
           F(I,NL_SVS+1)=0.0
        END IF
     END DO

  END IF


  !        6.      CALCULATE NEW SOIL WATER CONTENT FOR EACH LAYER
  !              -----------------------------------------
  !
  ! Compute water removed by evapotranspiration from each layer (rate in m/s, grid box average)
  DO I=1,N
     DO K=1,NL_SVS
        ETR_GRID(I,K)=((VEGL(I)*(1.-PSN(I))+VEGH(I)*(1.-PSNVH(I)))*ACROOT(I,K)*ETR(I))/(1000.*DELZ(K))
     END DO
  END DO
  !
  !Compute water fluxes between soil layers based on Richard's equation 

  ! Boundary condition at the top is net precipitation      
  DO I=1,N
     F(I,1)=(PG(I)-(1.-VEGL(I)-VEGH(I))*(1-PSN(I))* EG(I))*DT/1000.
  END DO

  !Compute water fluxes between soil layers, find K AND PSI at the boundaries     
  ! do it for all layers except KDP and NL_SVS (water flux is computed above from watdrain).       

  IF (hydro_svs_method.EQ.0) THEN
     ! First-order forward method       
     CALL SOIL_FLUXES( DT, &
          WSATC, KSATC, PSISAT, BCOEF, ETR_GRID, WD, &
          F, WDT, DWD_RK1, KHC, PSI, N)
  ELSE !hydro_svs_method=1
     ! Runge-Kutta 4th order method
     ! see for example http://lpsa.swarthmore.edu/NumInt/NumIntFourth.html
     ! Divide the source/sink terms by half to estimate WD at midpoint
     DO I=1,N
        DO K=1,NL_SVS+1

           F_RK(I,K) = F(I,K)/2.
        END DO
     END DO
     ! k1=f(y*(t0),t0)
     CALL SOIL_FLUXES( DT/2., &
          WSATC, KSATC, PSISAT, BCOEF, ETR_GRID, WD, &
          F_RK, WD_RK, DWD_RK1, KHC, PSI, N)      
     ! k2=(f(y*(t0)+k1*h/2,t0+h/2)
     CALL SOIL_FLUXES( DT/2., &
          WSATC, KSATC, PSISAT, BCOEF, ETR_GRID, WD_RK, &
          F_RK, WD_RK, DWD_RK2, KHC, PSI, N)
     ! k3=f(y*(t0)+k2*h/2,t0+h/2)
     CALL SOIL_FLUXES( DT, &
          WSATC, KSATC, PSISAT, BCOEF, ETR_GRID, WD_RK, &
          F, WD_RK, DWD_RK3, KHC, PSI, N)
     ! k4=f(y*(t0)+k3*h,t0+h)
     CALL SOIL_FLUXES( DT, &
          WSATC, KSATC, PSISAT, BCOEF, ETR_GRID, WD_RK, &
          F, WD_RK, DWD_RK4, KHC, PSI, N)
     ! y*(t0+h)=y*(t0)+(k1*h/6+k2*h/3+k3*h/3+k4*h/6)
     ! careful: DWD_RK1 and DWD_RK2 were calculated with a timestep h=DT/2
     ! whereas DWD_RK3 and DWD_RK4 was calculated with h=DT
     ! so k1*h/6=1/3*DWD_RK1, k2*h/3=2/3*DWD_RK2, k3*h/3=1/3*DWD_RK3 and k4*h/6=1/6*DWD_RK4
     DO I=1,N
        DO K=1,NL_SVS
           WDT(I,K)=WD(I,K)+( &
                DWD_RK1(I,K)/3.+ &
                DWD_RK2(I,K)*2./3.+ &
                DWD_RK3(I,K)/3.+ &
                DWD_RK4(I,K)/6.)
        END DO
     END DO
  END IF

  ! Check if the calculated WDT is between critical water (0.01) and wsat
  !if WDT is less than CRITWATER get water from the next layer
  !if WD is more than saturation, add eccess water to lateral flow or baseflow (F) 
  !update water flow (f) of each layer based on the new soil moisture

  DO I=1,N
     DO K=1,NL_SVS
        IF (WDT(I,K).LT.CRITWATER)  THEN

           ! if we are in the last soil layer, soil water content of the layer below cannot be updated
           IF(K.NE.NL_SVS) WDT(I,K+1)=WDT(I,K+1)- &
                (CRITWATER-WDT(I,K))*DELZ(K)/DELZ(K+1)
           F(I,K+1)=F(I,K+1)-(CRITWATER-WDT(I,K))*DELZ(K)
           WDT(I,K)=CRITWATER

        ELSEIF (WDT(I,K).GT.WSATC(I,K))  THEN

           IF (K.NE.KDP) THEN
              ! excess water removal via downward flux
              F(I,K+1)=F(I,K+1)+(WDT(I,K)-WSATC(I,K))*DELZ(K)
              ! if we are in the last soil layer, soil water content of the layer below cannot be updated
              IF(K.NE.NL_SVS) WDT(I,K+1)=WDT(I,K+1)+(WDT(I,K)-WSATC(I,K))*DELZ(K)/DELZ(K+1)
           ELSEIF (K.EQ.KDP) THEN
              ! excess water removal via lateral flow
              LATFLW(I,K)=(WDT(I,K)-WSATC(I,K))*DELZ(K)
           END IF
           WDT(I,K)=WSATC(I,K)         

        END IF
     END DO  
  END DO

  !        7.      CALCULATE LATERAL FLOW
  !              -----------------------------------------
  !calculate parameter needed for WATDRAIN based on new WDT 

  DO I=1,N
     DO K=1,NL_SVS
        ASAT0 (I,K) = WDT(I,K) / WSATC(I,K)     
     END DO
  END DO

  !Call WATDRAIN to calculated SUBFLW from each layer 

  DO K=1,NL_SVS   

     CALL RUNSVSDRN(DELZVEC(:,K),BCOEF(:,K),WSATC(:,K),GRKSAT(:,K),GRKEFL(:,K), &
          ASATFC(:,K),ASAT0(:,K),ASAT1,SUBFLW,BASFLW,SATSFC,N,1,N,DT)

     DO I=1,N

        IF(WDT(I,K).GT.WFCINT(I,K))THEN
           !check SUBFLW does not exceed the availabe water in the layer
           SUBFLW(I) = MAX(0.,MIN(WDT(I,K)*DELZ(K),SUBFLW(I)))
           LATFLW (I,K) = LATFLW(I,K)+SUBFLW(I)
           !remove LATFLW from the layer
           WDT(I,K)  = WDT(I,K)-SUBFLW(I)/DELZ(K)
        ENDIF

     END DO

  END DO

! peut-etre ajouter call phase change???

  !---------------------------------------------------
  ! check phase chages
  !--------------------------------------------------
  ! SET MINIMUM VALUES OF SOIL WATER AND MAKES SURE FROZEN SOIL WATER NON-NEGATIVE
  DO I=1,N
     DO K=1,NL_SVS
        WDT(I,K) = MAX(WDT(I,K),CRITWATER)
      ENDDO
  END DO
  !--------------------------------------------------------
  !--------------------------------------------------
  ! CALCULATE DIAGNOSTICS, CONVERT UNITS, AND ACCUMULATORS 

  DO I=1,N

     DO K=1,NL_SVS
        ! CONVERT TO MM
        LATFLW (I,K) = LATFLW(I,K) * M_TO_MM
     ENDDO

     DO K=1,NL_SVS+1
        ! CONVERT TO MM
        F(I,K) = F(I,K) * M_TO_MM           
     ENDDO

  END DO

  RETURN
END SUBROUTINE HYDRO_SVS
